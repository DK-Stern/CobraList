buildscript {
    dependencies {
        classpath 'org.mariadb.jdbc:mariadb-java-client:2.3.0'
    }
}

plugins {
    id 'java'
    id 'jacoco'
	id 'org.springframework.boot' version '2.1.5.RELEASE'
    id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'io.spring.dependency-management'

repositories {
    jcenter()
}

dependencies {
    implementation project(':backend:app-properties')
    implementation project(':backend:party:party-crud:party-crud-api')
    implementation project(':backend:party:party-crud:party-crud-adapter')
    implementation project(':backend:party:party-crud:party-crud-controller')
    implementation project(':backend:party:party-join:party-join-api')
    implementation project(':backend:party:party-join:party-join-adapter')
    implementation project(':backend:party:party-join:party-join-controller')
    implementation project(':backend:party:party-join:party-join-dataaccess-port')
    implementation project(':backend:party:party-join:party-join-dataaccess-adapter')
    implementation project(':backend:security')
    implementation project(':backend:user:user-controller')
    implementation project(':backend:user:userinfo')
    implementation project(':backend:user:userprincipal')
    implementation project(':backend:party-persistence:party-data-access')
    implementation project(':backend:party-persistence:party-domain')
    implementation project(':backend:token-provider')
    implementation project(':backend:streaming:streaming-api')
    implementation project(':backend:streaming:streaming-domain')
    implementation project(':backend:streaming:streaming-exceptions')
    implementation project(':backend:streaming:streaming-spotify-adapter')

	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:2.3.0'
}

group = 'sh.stern'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

def webappDir = "$rootProject.projectDir/frontend"

jar {
    
    manifest {
        attributes "Main-Class": "sh.stern.cobralist.CobraListApplication"
    }
    
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

sourceSets {
    main {
        resources {
            srcDirs = ["$webappDir/dist", "$projectDir/src/main/resources"]
        }
    }
}

processResources {
    dependsOn ":frontend:buildWebApp"
}

flyway {
    url = 'jdbc:mariadb://localhost:3306/cobralist'
    user = 'cobra'
    password = 'cobrapw'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test:' + springBootVersion
        testImplementation 'org.assertj:assertj-core:' + assertJVersion
    }
}

def subProjects = subprojects.findAll()

task('checkAll').dependsOn(subProjects.check)

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'

    dependsOn(subProjects.test)

    additionalSourceDirs = files(subProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subProjects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subProjects.sourceSets.main.output)
    executionData = files(subProjects.jacocoTestReport.executionData)

    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }

    reports {
        html.enabled = true // human readable
        xml.enabled = true // required by coveralls
    }
}